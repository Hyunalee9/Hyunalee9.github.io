---
title: "[36일차] 패키지"
excerpt: "아이티윌 0612_(2) "
categories:
      - ORACLE11g
tags:
      - ORACLE11g
      - TIL
last-modified-at: 2025-06-12T18:37
---


# 패키지

📍 **패키지 등장 배경**

→ 관련성 있는 프로그램을 하나로 묶어 관리하고 싶다.

```sql
-- 들어온 형식 매개 변수와 최댓값 비교해서 더 크면 FALSE를 리턴, 아니면 TRUE를 리턴하는 함수.
-- boolean, record, 배열 타입 -> PL/SQL 전용이다. RETURN되는 타입에 따라 SQL 에 적용하는 것 유의하자.

CREATE OR REPLACE FUNCTION hr.validate_comm(p_comm IN number)
RETURN boolean
IS
    v_comm number;
BEGIN
    SELECT max(commission_pct)  -- 0.4
    INTO v_comm
    FROM hr.employees;
    
    IF p_comm > v_comm THEN
            RETURN FALSE;
    ELSE
            RETURN TRUE;
    END IF;        
END validate_comm;
/

CREATE OR REPLACE PROCEDURE hr.reset_comm(p_comm IN number)
IS
    v_comm number  := 0.1;
BEGIN
    IF hr.validate_comm(p_comm) THEN
        dbms_output.put_line('OLD : '||v_comm);
        v_comm := p_comm;
        dbms_output.put_line('NEW : '||v_comm);
    ELSE
        raise_application_error(-20000, '기존 최고값을 넘을 수 없습니다.');
    END IF;
END reset_comm;
/

--
BEGIN
    hr.reset_comm(0.2);
END;
/

BEGIN
    hr.reset_comm(0.3);
END;
/

BEGIN
    hr.reset_comm(0.5); -- 오류 발생.
END;
/         
```

# 패키지

- 관련성 있는 서브 프로그램(프로시저, 함수), 변수, 데이터 타입, 예외 사항을 모아 놓은 프로그램
- Specification (Public, Global) 은 패키지의 인터페이스이다.
    - 패키지 외부에서 참조할 수 있는 변수, 상수, 커서, 예외사항, 데이터 타입, 서브 프로그램을 선언한다.
- Body (Private) 는 서브 프로그램에 대한 코드를 정의한다.
- Body의 생성자는 서브 프로그램에서만 (로컬 서브 프로그램, 바디) 사용한다.

## ▶️ Package Specification

```sql
CREATE OR REPLACE PACKAGE package_name
IS
		변수, 상수, 커서, 예외사항, 데이터 타입, 서브 프로그램
END package_name;
/
```

## ▶️ Package Body

```sql
CREATE OR REPLACE PACKAGE BODY package_name
IS
		Private 생성자(변수, 상수, 커서, 예외사항, 데이터 타입) 선언
		서브 프로그램 코드 정의
[BEGIN] -- Package Body의 Begin 절은 optional 하다. 		
END package_name;
/
```

📍 **만드는 순서**

1. **Package Specification (Public, Global), 선언 부분**

```sql
CREATE OR REPLACE PACKAGE hr.comm_pkg
IS
		g_comm number := 0.1;     
		-- 여기서 사용한 변수는 global 변수여서 execute hr.comm_pkg.g_comm := 0.2 처럼 사용 가능 
		-- 외부에서 사용해야 되는 Public 생성자 선언
		PROCEDURE reset_comm(p_comm IN number);  
END comm_pkg;
/
```

📍 **Global 변수 사용법 (세션이 열려있는 동안 계속 사용할 수 있다.)**

```sql
execute 패키지 이름.변수이름
```

📍 **Global 변수 호출해보기**

```sql
execute hr.comm_pkg.g_comm := 0.2 
execute dbms_output.put_line(hr.comm_pkg.g_comm)  -- .2
```

1. **Body(Private),  실제 로직 프로그램 작성**

```sql
CREATE OR REPLACE PACKAGE hr.comm_pkg
IS
		g_comm number := 0.1;     
		-- 여기서 사용한 변수는 global 변수여서 execute hr.comm_pkg.g_comm := 0.2 처럼 사용 가능 
		-- 외부에서 사용해야 되는 생성자 선언
		PROCEDURE reset_comm(p_comm IN number);  
END comm_pkg;
/   

execute hr.comm_pkg.g_comm := 0.2 
execute dbms_output.put_line(hr.comm_pkg.g_comm)  -- .2

```

```sql

CREATE OR REPLACE PACKAGE BODY hr.comm_pkg
IS
	FUNCTION validate_comm(p_comm IN number)
    RETURN boolean
  IS
    v_comm number;
 BEGIN
    SELECT max(commission_pct)  -- 0.4
    INTO v_comm
    FROM hr.employees;
    
    IF p_comm > v_comm THEN
            RETURN FALSE;
    ELSE
            RETURN TRUE;
    END IF;        
 END validate_comm;

PROCEDURE reset_comm(p_comm IN number)
IS
		BEGIN
		    IF validate_comm(p_comm) THEN
		        dbms_output.put_line('OLD : '||g_comm);
		        g_comm := p_comm;
		        dbms_output.put_line('NEW : '||g_comm);
		    ELSE
		        raise_application_error(-20000, '기존 최고값을 넘을 수 없습니다.');
		    END IF;
		END reset_comm;

END comm_pkg;
/
```

▶️ **HR 세션 1**

```sql
-- SQL PLUS
SQL> execute hr.comm_pkg.reset_comm(0.2)

/*
OLD : .1
NEW : .2
*/

SQL> execute hr.comm_pkg.reset_comm(0.15)

/*
OLD : .2
NEW : .15
*/
```

▶️ **HR 세션2**

```sql
SQL> execute hr.comm_pkg.reset_comm(0.3)

/*
OLD : .1
NEW : .3
*/

SQL> execute hr.comm_pkg.reset_comm(0.5)
BEGIN hr.comm_pkg.reset_comm(0.5); END;

/*
ERROR at line 1:
ORA-20000: 기존 최고값을 넘을 수 없습니다.
ORA-06512: at "HR.COMM_PKG", line 28
ORA-06512: at line 1
*/
```

📍 알파벳 순으로  정의하기 위해 위치를 바꿨다. → 오류남 

→ package body에 정의되는 생성자는 전방 참조만 가능하다.  ex) 클래스.

→ 이럴 경우 header (IS 윗부분) 부분만 복사 붙여 넣기 하면 오류없이 작동된다. 

```sql
CREATE OR REPLACE PACKAGE BODY hr.comm_pkg
IS
 FUNCTION validate_comm(p_comm IN number)
    RETURN boolean;

	PROCEDURE reset_comm(p_comm IN number)
IS
		BEGIN
		    IF validate_comm(p_comm) THEN
		        dbms_output.put_line('OLD : '||g_comm);
		        g_comm := p_comm;
		        dbms_output.put_line('NEW : '||g_comm);
		    ELSE
		        raise_application_error(-20000, '기존 최고값을 넘을 수 없습니다.');
		    END IF;
		END reset_comm;
	
	FUNCTION validate_comm(p_comm IN number)
    RETURN boolean
  IS
    v_comm number;
 BEGIN
    SELECT max(commission_pct)  -- 0.4
    INTO v_comm
    FROM hr.employees;
    
    IF p_comm > v_comm THEN
            RETURN FALSE;
    ELSE
            RETURN TRUE;
    END IF;        
 END validate_comm;

END comm_pkg;
/
```

## Package overloading

- 동일한 이름의 생성자(프로시저, 함수) 를 만들 수 있다.
- 형식 파라미터의 갯수, 모드(IN, OUT, IN OUT), 데이터 유형이 다를 경우 동일한 이름의 서브 프로그램을 생성할 수 있다.

```sql
CREATE OR REPLACE PACKAGE hr.pack_over
IS
		TYPE date_tab_type IS TABLE OF date INDEX BY pls_integer;
		TYPE num_tab_type IS TABLE OF number INDEX BY pls_integer;
		
		PROCEDURE init(tab OUT date_tab_type, n IN number); 
		PROCEDURE init(tab OUT num_tab_type, n IN number);
END pack_over;
/
```

```sql
CREATE OR REPLACE PACKAGE BODY hr.pack_over
IS
	PROCEDURE init(tab OUT date_tab_type, n IN number)
	IS
	BEGIN
			FOR i IN 1..n LOOP
					tab(i) := sysdate;
			END LOOP;
	END;
	
	PROCEDURE init(tab OUT num_tab_type, n IN number)
	IS
	BEGIN
		  FOR i IN 1..n LOOP
					tab(i) := i;
			END LOOP;
	END;
END pack_over;
```

```sql
DECLARE
    date_tab hr.pack_over.date_tab_type;
    num_tab hr.pack_over.num_tab_type;
BEGIN
    hr.pack_over.init(date_tab,5);
    hr.pack_over.init(num_tab,5);
    
    FOR i IN 1..5 LOOP
        dbms_output.put_line(date_tab(i));
        dbms_output.put_line(num_tab(i));
    END LOOP;
END;
/
```

## 상수 표준화

▶️ 1  mile = 1.6093 kilo

▶️ 1  kilo = 0.6214 mile

▶️ 1 yard = 0.9144 meter

▶️ 1 meter = 1.0936 yard

```sql
CREATE OR REPLACE PACKAGE hr.global_conts
IS
		c_mile_2_kilo constant number  := 1.6093;
		c_kilo_2_mile constant number  := 0.6214;
		c_yard_2_meter constant number := 0.9144;
		c_meter_2_yard constant number := 1.0936;
END global_conts;
/

BEGIN
	   dbms_output.put_line('20 mile = '||20 * hr.global_conts.c_mile_2_kilo||'km');
END;
/

-- 20 mile = 32.186km

BEGIN
	   dbms_output.put_line('20 kilo = '||20 * hr.global_conts.c_yard_2_meter||'m');
END;
/

-- 20 kilo = 18.288m

BEGIN
	   dbms_output.put_line('20 meter = '||20 * hr.global_conts.c_meter_2_yard||'yd');
END;
/

-- 20 meter = 21.872yd

CREATE OR REPLACE FUNCTION hr.mtr_to_yrd(p_m IN number)
RETURN number
IS
BEGIN
    RETURN(p_m * hr.global_conts.c_meter_2_yard);
END mtr_to_yrd;
/

execute dbms_output.put_line(hr.mtr_to_yrd(100));

BEGIN
	   dbms_output.put_line('20 mile = '||20 * hr.global_conts.c_mile_2_kilo||'km');
END;
/
```

## EXCEPTION 이름 표준화

```sql
CREATE OR REPLACE PACKAGE hr.err_pkg
IS
    notnull_err exception;
    pragma exception_init(notnull_err, -1400);
END err_pkg;
/

desc hr.departments

BEGIN
    INSERT INTO hr.departments(department_id, department_name) VALUES(300,null);
EXCEPTION
    WHEN hr.err_pkg.notnull_err THEN
        dbms_output.put_line('필수 항목 값을 꼭 입력해 주세요.');
        dbms_output.put_line(sqlerrm);
END;
/
```