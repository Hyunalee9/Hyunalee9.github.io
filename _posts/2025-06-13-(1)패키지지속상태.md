---
title: "패키지 커서 지속 상태"
excerpt: "아이티윌 0613_(1) "
categories:
      - ORACLE11g
tags:
      - ORACLE11g
      - TIL
last-modified-at: 2025-06-13T18:37
---

📍**패키지 사용 이유** 

- **연관성** 있는 서브 프로그램등을 묶어 사용하기 위해
- 종속성 표현
- global 변수 사용

 📍 **종속 관계** - 참조하는 관계가 **invalid** 이면 -> 모두 **invalid** (실행 불가 상태)
       ex) view
      내가 관리하고 있는 객체의 status 체크가 DBA의 데일리 루틴...이라고 한다.

```sql
-- db에 저장되어있는 모든 객체들을 조회한다.
-- 이걸 왜 말한겨

SELECT *
FROM dba_objects;
```

# 패키지 커서 지속 상태

- **세션 종료 시점까지 지속**된다.
- 커서는 **서버**에 만들어진다.

> 📍 커서 : SQL 실행 메모리 영역. user process에서 SQL 문 날리면 server process에서 받는다. library cache 라는 곳에 PGA라는 곳에 UGA라는 곳 안에 커서가 있다. SELECT 문의 실행 영역 - **Parse (syntax, semaintc) - Bind - Execute - Fetch**
> 

- 어디에 선언하느냐에 따라 public, private, local 성격을 가지게 된다.

```sql
-- Spec
CREATE OR REPLACE PACKAGE hr.pack_cur
IS
	  PROCEDURE open;
	  PROCEDURE next(p_num IN number);
	  PROCEDURE close;
END pack_cur;
/

-- Body
CREATE OR REPLACE PACKAGE BODY hr.pack_cur
IS
		/* private cursor package body 안에서 어디서든지 사용할 수 있다.*/
	  CURSOR c1 IS
      SELECT employee_id, last_name
      FROM hr.employees
      ORDER BY employee_id desc;
			
		/* private variable : package body안에서는 어디서든지 사용할 수 있다. */		  
    v_empno number;
    v_ename varchar2(30);
		
    PROCEDURE open
    IS
    BEGIN 
			IF NOT c1%ISOPEN THEN
					  OPEN c1;
					  dbms_output.put_line('c1 cursor open'); 
			END IF;		  
    END open;
		
      PROCEDURE next(p_num IN number)
      IS
      BEGIN
				LOOP
					 EXIT WHEN c1%ROWCOUNT >= p_num OR c1%NOTFOUND;
					 FETCH c1 INTO v_empno, v_ename;
					 dbms_output.put_line(v_empno||' '||v_ename);
				END LOOP;
      END next;
		
       PROCEDURE close
       IS
       BEGIN
				IF c1%ISOPEN THEN
					 CLOSE c1;
					 dbms_output.put_line('c1 curcor close');
                END IF;     
      END close;		
END pack_cur;
/				  
```

▶️ open 전 fatch 가 돌아가게 되면 오류남

```sql
execute hr.pack_cur.next(3)

/*
ERROR at line 1:
ORA-01001: invalid cursor
ORA-06512: at "HR.PACK_CUR", line 24
ORA-06512: at line 1
*/
```

▶️ open 후 fatch

```sql
SQL> execute hr.pack_cur.open
--  c1 cursor open

SQL> execute hr.pack_cur.next(3)

/*
206 Gietz
205 Higgins
204 Baer
*/

-- 포인터는 4번째에 위치해있음. 커서 아직 살아있음.
-- 다음 3건을 뽑아야 한다면 3을 지정하지 않고 +3을 더해야 한다.

SQL> execute hr.pack_cur.next(6)
/*
203 Mavris
202 Fay
201 Hartstein
*/

SQL> execute hr.pack_cur.next(9)

/*
200 Whalen
199 Grant
198 OConnell
*/

SQL> execute hr.pack_cur.close
-- c1 curcor close

```

📍 무한 루프 탈출 : Ctrl + C 

## Global 변수 초기값으로 되돌리는 방법

- specification 에서 선언한 public(global) 변수는 호출이 끝나면 초기값으로 되돌아 가도록 수행해야 한다.
- PRAGMA SERIALLY_REUSABLE

```sql
/*Spec*/
CREATE OR REPLACE PACKAGE hr.comm_pkg
IS
    PRAGMA SERIALLY_REUSABLE;
		g_comm number := 0.1;     
		-- 여기서 사용한 변수는 global 변수여서 execute hr.comm_pkg.g_comm := 0.2 처럼 사용 가능 
		-- 외부에서 사용해야 되는 Public 생성자 선언
		PROCEDURE reset_comm(p_comm IN number);  
END comm_pkg;
/
```

```sql
/*Body*/
CREATE OR REPLACE PACKAGE BODY hr.comm_pkg
IS
 PRAGMA SERIALLY_REUSABLE;   -- 원래 값으로 되돌리는 방법
 FUNCTION validate_comm(p_comm IN number)
    RETURN boolean;

	PROCEDURE reset_comm(p_comm IN number)
IS
		BEGIN
		    IF validate_comm(p_comm) THEN
		        dbms_output.put_line('OLD : '||g_comm);
		        g_comm := p_comm;
		        dbms_output.put_line('NEW : '||g_comm);
		    ELSE
		        raise_application_error(-20000, '기존 최고값을 넘을 수 없습니다.');
		    END IF;
		END reset_comm;
	
	FUNCTION validate_comm(p_comm IN number)
    RETURN boolean
  IS
    v_comm number;
 BEGIN
    SELECT max(commission_pct)  -- 0.4
    INTO v_comm
    FROM hr.employees;
    
    IF p_comm > v_comm THEN
            RETURN FALSE;
    ELSE
            RETURN TRUE;
    END IF;        
 END validate_comm;

END comm_pkg;
/
```

```sql
-- SQL PLUS 창
SQL> execute dbms_output.put_line(hr.comm_pkg.g_comm)
-- .1

PL/SQL procedure successfully completed.

SQL> execute hr.comm_pkg.reset_comm(0.2)

/*
OLD : .1
NEW : .2

PL/SQL procedure successfully completed.
*/

SQL> execute dbms_output.put_line(hr.comm_pkg.g_comm)
-- .1    > reset_comm이라는 함수 기능을 완료하고 원래 값으로 다시 돌아왔다.
```