---
title: "[32일차] 조합 데이터 유형"
excerpt: "아이티윌 0605_(2) "
categories:
      - ORACLE11g
tags:
      - ORACLE11g
      - TIL
last-modified-at: 2025-06-05T19:18
---



# 조합 데이터 유형

- 스칼라 유형(scalar data type) : 단일값을 보유하는 변수
- 스칼라 유형과 달리 다중값을 보유할 수 있는 변수
- 레코드 (record) : 서로 다른 데이터 유형의 값을 저장하고자 할 때
- 배열 (array) : 동일한 유형의 값을 저장
    - index by table(연관 배열)
    - nested table(중첩 테이블)
    - varray

```sql
DECLARE
    /*레코드 타입 선언, field(필드)*/
     TYPE dept_record_type IS RECORD
    (dept_id departments.department_id%type,
    dept_name departments.department_name%type,
    dept_mgr departments.manager_id%type,
    dept_loc departments.location_id%type);
    
    v_rec dept_record_type;
    
BEGIN
    SELECT *
    INTO v_rec
    FROM hr.departments
    WHERE department_id = 10;
    dbms_output.put_line('부서번호 : '||v_rec.dept_id);
    dbms_output.put_line('부서이름 : '||v_rec.dept_name);
    dbms_output.put_line('부서장 : '||v_rec.dept_mgr);
    dbms_output.put_line('부서위치 : '||v_rec.dept_loc);
END;
/
```

▶️ 컬럼 갯수가 늘어나면 일일히 선언하기 어려울 수 있다.

```sql
DECLARE
    TYPE rec_type IS RECORD
    ( sal         number,
      minsal      number default 1000,
      day         employees.hire_date%type,
      rec         employees%rowtype);
      
    v_rec rec_type;  
BEGIN
    v_rec.sal := v_rec.minsal + 500;
    v_rec.day := sysdate;
    
    SELECT *
    INTO v_rec.rec
    FROM hr.employees
    WHERE employee_id = :b_id;
    
    dbms_output.put_line(v_rec.rec.first_name);
    dbms_output.put_line(trunc(months_between(v_rec.day,v_rec.rec.hire_date)/12));
END;
/
```

 ➡ **%rowtype 사용.**

📍 **%rowtype**

- 테이블 또는 뷰의 열에서 컬럼 및 데이터 타입을 가지고 레코드 타입을 선언한다.
- 레코드 타입을 손쉽게 만들 수 있음.
- 컬럼 갯수와 타입을 알 필요가 없다.
- SELECT * 문
- 행 레벨 INSERT, UPDATE문

예제 1)

```sql
DECLARE
    
    v_rec departments%rowtype;
    
BEGIN
    SELECT *
    INTO v_rec
    FROM hr.departments
    WHERE department_id = 10;
    dbms_output.put_line('부서번호 : '||v_rec.department_id);
    dbms_output.put_line('부서이름 : '||v_rec.department_name);
    dbms_output.put_line('부서장 : '||v_rec.manager_id);
    dbms_output.put_line('부서위치 : '||v_rec.location_id);
END;
/

```

예제 2)

```sql
-- 테이블 만들기
CREATE TABLE hr.retired_emp
(no   number,
name  varchar2(30),
job   varchar2(30),
mgr   number,
hiredate date,
leavedate date,
sal number,
comm number,
deptno number);

-- 
DECLARE
    **v_rec** retired_emp%rowtype;
BEGIN
    SELECT employee_id, last_name, job_id, manager_id, hire_date, sysdate, salary, commission_pct, department_id
    INTO **v_rec**
    FROM hr.employees 
    WHERE employee_id = 116;

    INSERT INTO hr.retired_emp VALUES v_rec;  -- 행 레벨 IBSERT 가능 (구조가 동일하다면)
    
    COMMIT;
END;
/
-- 레코드 변수의 모습과 이 테이블 구조가 동일하다면 행 레벨의 insert가 가능하다.  

SELECT * FROM hr.retired_emp;
```

➡ 한 행만 수정 작업할 때.

```sql

-- 한 행만 수정 적업할 때

DECLARE
    v_rec retired_emp%rowtype;
BEGIN
    SELECT employee_id, last_name, job_id, manager_id, hire_date, sysdate, salary, commission_pct, department_id
    INTO v_rec
    FROM hr.employees
    WHERE employee_id = 116;
    
    v_rec.name := upper(v_rec.name);
    v_rec.leavedate := to_date('2025-06-01','yyyy-mm-dd');
    v_rec.comm := 0.1;
    
    /* 행 레벨 UPDATE */
    UPDATE hr.retired_emp
    SET row = v_rec
    WHERE no = 116;
END;
/
```

## 연관 배열 (associative array, index by table)

- 2개의 열을 포함하는 collection 이다.
- key, value집합으로 구성되어 있다.
- key : 정수 (pls_integer(-2G ~ 2G)), 문자열(varchar2(32767(11g),65536(12c))) 데이터 유형의 primary key(unique, not null)
- value : 스칼라, 레코드 데이터 유형

**📍 배열 메소드**

- count : 배열이 포함하는 값의 수
- first : 배열의 첫번째(가장 작은 번호) 인덱스 번호(방번호)
- last : 배열의 마지막(가장 큰 번호) 인덱스 번호(방번호)
- next : 배열 인덱스 n번 뒤에 오는 인덱스 번호
- prior : 배열 인덱스 n번 앞에 오는 인덱스 번호
- delete(n) : n번 값을 삭제
- delete(m,n) : m번 부터 n번까지 값을 삭제
- delete : 배열 모든 값을 삭제

예제 1)

```sql
DECLARE
    -- TYPE 선언할 데이터 타입 이름 IS TABLE OF varchar2(10)(-> 각 요소는 이 타입이다)
    -- INDEX BY pls_integer 를 인덱스 키로 사용한다. 
		TYPE tab_char_type IS TABLE OF varchar2(10) INDEX BY pls_integer;
		v_city tab_char_type;
BEGIN
		v_city(1) := '서울';
		v_city(2) := '대전';
		v_city(3) := '경기';
		v_city(4) := '부산';
		dbms_output.put_line(v_city.count);
		dbms_output.put_line(v_city.first);
		dbms_output.put_line(v_city.last);
		dbms_output.put_line(v_city.next(1));
    dbms_output.put_line(v_city.prior(3));
		
END;
/

/*
4
1
4
2
2
*/

```

예제 2)

```sql
DECLARE
	TYPE tab_char_type IS TABLE OF varchar2(10) INDEX BY pls_integer;
	v_city tab_char_type;
BEGIN
		v_city(4) := '서울';
		v_city(2) := '대전';
		v_city(3) := '경기';
		v_city(1) := '부산';
		
		v_city.delete(2);
		FOR i IN v_city.first..v_city.last LOOP   -- 오류 발생
				dbms_output.put_line(v_city(i));
		END LOOP;
END;
/

-->

DECLARE
	TYPE tab_char_type IS TABLE OF varchar2(10) INDEX BY pls_integer;
	v_city tab_char_type;
BEGIN
		v_city(4) := '서울';
		v_city(2) := '대전';
		v_city(3) := '경기';
		v_city(1) := '부산';
		
		v_city.delete(2);
		FOR i IN v_city.first..v_city.last LOOP
              IF  v_city.exists(i) then
				dbms_output.put_line(v_city(i));
              ELSE
                dbms_output.put_line(i||'key-value가 존재하지 않습니다.');
              END IF;  
		END LOOP;
END;
/

/*

부산
2key-value가 존재하지 않습니다.
경기
서울

*/
```

예제 3)

```sql

DECLARE
		TYPE tab_char_type IS TABLE OF varchar2(10) INDEX BY pls_integer;
		v_city tab_char_type;
BEGIN
		v_city(1) := '서울';
		v_city(2) := '대전';
		v_city(3) := '경기';
		v_city(4) := '부산';
		dbms_output.put_line(v_city(1));
		dbms_output.put_line(v_city(2));
		dbms_output.put_line(v_city(3));
		dbms_output.put_line(v_city(4));
		
END;
/

/*

서울
대전
경기
부산

*/
```